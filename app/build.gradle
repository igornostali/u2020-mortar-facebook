buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.10.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'hugo'

dependencies {
    compile 'com.android.support:support-v4:21.0.0'

    compile 'com.squareup.mortar:mortar:0.16'
    compile 'com.squareup.flow:flow:0.7'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.squareup.retrofit:retrofit:1.6.0'

    compile 'com.netflix.rxjava:rxjava-core:0.17.4'
    compile 'com.netflix.rxjava:rxjava-android:0.17.4'

    compile 'com.squareup.dagger:dagger:1.2.1'
    compile 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.jakewharton:butterknife:5.0.1'
    compile 'com.jakewharton.timber:timber:2.2.2'

    compile 'com.etsy.android.grid:library:1.0.3'

    debugCompile 'com.squareup.retrofit:retrofit-mock:1.6.0'
    debugCompile 'com.jakewharton.madge:madge:1.1.1'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
    debugCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.squareup.dagger'
    }

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.1'
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    
    compileSdkVersion sdk.compileSdkVersion
    buildToolsVersion sdk.buildToolsVersion

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion sdk.minSdkVersion
        targetSdkVersion sdk.targetSdkVersion

        versionCode app.buildVersion.major * 100 + app.buildVersion.minor * 10 + app.buildVersion.patch
        versionName "${app.buildVersion.major}.${app.buildVersion.minor}.${app.buildVersion.patch}"

        buildConfigField "String", "GIT_SHA", "\"${app.info.gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${app.info.buildTime}\""
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

spoon {
    debug = true
    noAnimations = true

    if (project.hasProperty('spoonClassName')) {
        className = project.spoonClassName

        if (project.hasProperty('spoonMethodName')) {
            methodName = project.spoonMethodName
        }
    }

    if (project.hasProperty('spoonTestSize')) {
        testSize = project.spoonTestSize
    }
}
